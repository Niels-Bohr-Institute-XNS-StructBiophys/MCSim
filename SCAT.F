      PROGRAM SCAT
C*******************************************************************
C     This program reads a set of coordinates (b,x,y,z) from a 
C     specified file generated by SIMUL.FOR, where (x,y,z) is the 
C     cartesian coordinates for the scatterer and b is the 
C     corresponding scattering length. 
C     When the program has calculated the
C     distance distribution function, it is written to a file
C     (of extension *dist.dat) and Fourier transformed to give the
C     scattering profile, which is written to the specified file.
C********************************************************************
      PARAMETER (nNTOT=20000, nNDAT=20001)
      DIMENSION B(nntot), X(nntot), Y(nNTOT), Z(nNTOT)
      DIMENSION COR(0:nNTOT),FINT(0:nNTOT),ERR(0:nNTOT)
      CHARACTER*20 ANAME,BNAME
      CHARACTER*24 DNAME
      INTEGER*4 ICPU,ICPUH,ICPUM
      DATA COR/nndat*0./,FINT/20001*0./,ERR/20001*0./
      PI=3.14159
C********************************************************************
C      Input
C********************************************************************
      I=-1
      WRITE(6,4)
    4 FORMAT(1X,'Inputfile                  => ',$)
      READ(5,5)ANAME
    5 FORMAT(A)
      WRITE(6,7)
    7 FORMAT(1X,'Outputfile                 => ',$)
      READ(5,6)BNAME
      DNAME=ANAME//'DIST'
    6 FORMAT(A)
      WRITE(6,8)
    8 FORMAT(1X,'Approx no points in output => ',$)
      READ(5,*)NPOINTS
      OPEN(UNIT=2,NAME=ANAME,status='OLD')
      WRITE(6,2)
    2 FORMAT(1X,'Qmax                       => ',$)
      READ(5,*)QMAX
      XMAX=-1.E30
      XMIN=-XMAX
      YMIN=XMIN
      YMAX=XMAX
      ZMIN=XMIN
      ZMAX=XMAX
   10 DO 15 I=1,nNTOT
      READ(2,*,ERR=990,END=20)B(I),X(I),Y(I),Z(I)
      XMIN=MIN(XMIN,X(I))
      XMAX=MAX(XMAX,X(I))
      YMIN=MIN(YMIN,Y(I))
      YMAX=MAX(YMAX,Y(I))
      ZMIN=MIN(ZMIN,Z(I))
      ZMAX=MAX(ZMAX,Z(I))
   15 CONTINUE
   20 CLOSE(UNIT=2)
      NTOT=I-1
      WRITE(6,25)NTOT
      write(6,24)xmin,xmax,ymin,ymax,zmin,zmax
   24 FORMAT(1X,'Dimensions : ',6F8.1)
   25 FORMAT(1X,'No of scatterers              =   ',I5)
c      ICPU=NTOT**2*2./1.E6*.5
c      ICPUM=MOD(ICPU,60)
c      ICPUH=(ICPU-ICPUM)/60
c      WRITE(6,26)ICPUH,ICPUM
c   26 FORMAT(1X,'Expected CPU-time (microvax)  =   ',I2,' h ',I2,' m')
      RMAX=SQRT((XMAX-XMIN)**2+(YMAX-YMIN)**2+(ZMAX-ZMIN)**2)
      RSTEP=RMAX/NTOT
      WRITE(6,35)RSTEP
   35 FORMAT(1X,'Steplength for dd-function    = ',F10.4)
C*********************************************************************
C     Guinier-radius
C*********************************************************************
      CMX=0
      CMY=0
      CMZ=0
      BTOT=0
      DO 210 I=1,NTOT
      CMX=CMX+B(I)*X(I)
      CMY=CMY+B(I)*Y(I)
      CMZ=CMZ+B(I)*Z(I)
      BTOT=BTOT+B(I)
  210 CONTINUE
      CMX=CMX/BTOT
      CMY=CMY/BTOT
      CMZ=CMZ/BTOT
      WRITE(6,211)CMX,CMY,CMZ
  211 FORMAT(1X,'Center of (scattering-)mass   = ',3(F8.2))
      RG=0
      DO 220 I=1,NTOT
      RG=RG+B(I)*((X(I)-CMX)**2+(Y(I)-CMY)**2+(Z(I)-CMZ)**2)
  220 CONTINUE
      RG=SQRT(RG/BTOT)
      WRITE(6,221)RG
  221 FORMAT(1X,'Guinier-radius                = ',F8.2)
C*********************************************************************
C     Distance distribution function
C*********************************************************************
      NDIV=MOD(NTOT,2)
      NTOT=NTOT-NDIV
      DO 101 J=1,NTOT-1,2
      DO 100 K=2,NTOT,2
      DXX=X(J)-X(K)
      DYY=Y(J)-Y(K)
      DZZ=Z(J)-Z(K)
      D=SQRT(DXX*DXX+DYY*DYY+DZZ*DZZ)
      L=NINT(D/RSTEP)
      COR(L)=COR(L)+B(J)*B(K)
  100 CONTINUE
  101 CONTINUE
      OPEN(UNIT=3,NAME='dist.d',status='UNKNOWN')
      NMAX=0
      DO 105 I=1,NTOT
      IF(COR(I).NE.0) NMAX=I
  105 CONTINUE
      WRITE(6,130)NMAX*RSTEP
  130 FORMAT(1X,'Maximum length of molecule    = ',F10.3)
      COR(0)=2*COR(0)
      COR(NMAX)=2*COR(NMAX)
      NTOT=NMAX
      DO 200 J=0,NMAX  
      WRITE(3,*)J*RSTEP,COR(J),SQRT(COR(J))
  200 CONTINUE
      CLOSE(UNIT=3)       
C*********************************************************************
C     Fouriertransformation => scattering profile
C*********************************************************************
      WRITE(6,222)QMAX
  222 FORMAT(1X,'Qmax at Fouriertransformation =   ',F7.4)
      QSTEP=QMAX/NPOINTS
      CALL FT(COR,RSTEP,FINT,QSTEP,NMAX,NPOINTS,ERR)
C*********************************************************************
C     Output
C*********************************************************************
      OPEN(UNIT=3,NAME=BNAME,status='UNKNOWN')
      DO 500 I=0,NPOINTS
      WRITE(3,*)I*QSTEP,FINT(I)/FINT(0),ERR(I)/FINT(0)
  500 CONTINUE
      CLOSE(UNIT=3)
      goto 999
 
  990 WRITE(6,*)'ERROR AT I=',I
 
  999 STOP
      END
 
**********************************************************************
*     Fouriertransformation
**********************************************************************
      SUBROUTINE FT(COR,RSTEP,FINT,QSTEP,NTOT,NPOINTS,ERR)
      REAL COR(0:NTOT),FINT(0:NTOT),ERR(0:NTOT)
      DO 400 K=1,NPOINTS                              
      Q=K*QSTEP
      FADD=0.
      EADD=0.
      DO 300 I=1,NTOT                               
      R=I*RSTEP
      DEBEYE=SIN(Q*R)/(Q*R)
      FADD=COR(I)*DEBEYE
      FINT(K)=FINT(K)+FADD
      EADD=DEBEYE*COR(I)*DEBEYE
      ERR(K)=ERR(K)+EADD
  300 CONTINUE
      FINT(K)=FINT(K)+COR(0)*0.5                    
      ERR(K) =ERR(K) +COR(0)*0.5
  400 CONTINUE
      DO 500 I=1,NTOT
      FINT(0)=FINT(0)+COR(I)
      ERR(0) =ERR(0) +COR(I)
  500 CONTINUE
      FINT(0)=FINT(0)+0.5*COR(0)
      ERR(0) =ERR(0) +0.5*COR(0)
      DO 600 I=0,NPOINTS
      ERR(I)=SQRT(ERR(I))
  600 CONTINUE
      RETURN
      END
